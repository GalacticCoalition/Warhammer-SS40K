// Hijacks the manifest_inject proc to allow for off-manifest roles.
/datum/datacore/manifest_inject(mob/living/carbon/human/human, client/human_client)
	var/datum/background_info/background_info = GLOB.employments[human.employment]
	var/assignment = human.mind.assigned_role.title

	if(/datum/background_feature/off_manifest in background_info.features)
		var/datum/data/record/record = new
		record.fields["name"] = human.real_name
		record.fields["rank"] = human_client?.prefs.alt_job_titles[assignment] || assignment
		// This will be force generated by passports anyways, so sorting out the appearance var is just a waste of cycles.
		var/datum/picture/picture = new
		picture.picture_name = "[human]"
		picture.picture_desc = "This is [human]"
		picture.picture_image = get_flat_existing_human_icon(human, list(SOUTH))
		record.fields["photo_front"] = new /obj/item/photo(null, picture)
		GLOB.ghost_records += record
		GLOB.name_to_datacore_entry += list("[human.real_name]" = list("general" = record))
		world.log << "Hidden user!"
		return

	return ..()

