/obj/item/disk/nifsoft_uploader/summoner
	loaded_nifsoft = /datum/nifsoft/summoner


/datum/nifsoft/summoner
	name = "Grimoire Caeruleam"
	program_desc = "The Grimoire Caeruleam is an open-source, virtual decentralized directory of summonable objects originally developed by the Altspace Coven, a post-pagan group of witches first digitized into engrams in the year 2544. These summonable constructs, or 'Icons,' are comprised of delicate patterns of nanomachines serving as a framework and projector for hardlight; the name 'Caeruleam' referencing the blue light an Icon casts in the user's hand. While the Grimoire has served thousands thus far, Corporate interests have blocked all access to Icons capable of harming their assets."
	cost = 200
	activation_cost = 100 // Around 1/10th the energy of a standard NIF
	/// Does the resulting object have a holographic like filter appiled to it?
	var/holographic_filter = TRUE
	/// Is there any special tag added at the begining of the resulting object name?
	var/name_tag = "nanite constructed "
	cooldown = TRUE

	///The list of items that can be summoned from the NIFSoft.
	var/list/summonable_items = list(
		/obj/item/toy/katana/nanite,
		/obj/item/cane/nanite,
		/obj/item/storage/dice,
		/obj/item/toy/cards/deck,
		/obj/item/toy/cards/deck/tarot, //The arcana is the means by which all is revealed
		/obj/item/toy/cards/deck/kotahi,
		/obj/item/toy/cattoy,
		/obj/item/toy/foamblade,
	)

/datum/nifsoft/summoner/activate()
	var/obj/item/organ/internal/cyberimp/brain/nif/installed_nif = parent_nif
	var/mob/living/carbon/human/linked_human = installed_nif.linked_mob

	if(!activation_check(installed_nif))
		return FALSE

	var/list/summon_choices = list()
	for(var/obj/item/summon_item as anything in summonable_items)
		var/image/obj_icon = image(icon = initial(summon_item.icon), icon_state = initial(summon_item.icon_state))

		summon_choices[summon_item] = obj_icon

	var/obj/item/choice = show_radial_menu(linked_human, linked_human, summon_choices, radius = 42, custom_check = CALLBACK(src, .proc/check_menu, linked_human))
	if(!choice)
		return FALSE

	var/obj/item/new_item = new choice

	new_item.nif_generated_item = TRUE
	if(name_tag)
		new_item.name = name_tag + new_item.name //This is here so that people know the item is created from a NIF.

	if(holographic_filter)
		new_item.alpha = 180
		new_item.set_light(2)
		new_item.add_atom_colour("#acccff", FIXED_COLOUR_PRIORITY)

	if(!linked_human.put_in_hands(new_item))
		to_chat(linked_human, span_warning("The [new_item] fails to materialize in your hands!"))
		qdel(new_item)
		return FALSE

	return ..()

/datum/nifsoft/summoner/proc/check_menu(mob/living/carbon/human/user)
	if(!istype(user) || !user.installed_nif)
		return FALSE

	return TRUE

/obj/item
	/// Is the item generated by a NIF?
	var/nif_generated_item = FALSE

/obj/item/examine(mob/user)
	. = ..()

	if(nif_generated_item)
		. += span_blue("<b>Ctrl + Shift + Click</b> to destroy this item.")

/obj/item/CtrlShiftClick(mob/user)
	. = ..()

	if(nif_generated_item)
		user.visible_message(span_blue("[usr] firmly grasps the [src], causing it to disintegrate into a fine blue ash"),
		span_blue("You firmly grasp the [src], causing it to disintegrate into a fine blue ash"))
		qdel(src)

//Summonable Items
///A somehow wekaer version of the toy katana
/obj/item/toy/katana/nanite
	force = 0
	throwforce = 0

///A nanite version of the cane.
/obj/item/cane/nanite
	force = 0
	throwforce = 0
